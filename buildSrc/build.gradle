plugins {
    id 'groovy-gradle-plugin'
}

ext {
    flexmarkVersion = '0.64.8'
    micronautVersion = '4.10.4'
}

repositories {
    mavenCentral()
}

dependencies {

    implementation platform("io.micronaut.platform:micronaut-platform:$micronautVersion")

    implementation "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-tables:$flexmarkVersion"
    implementation 'io.micronaut.rss:micronaut-rss-core'
    implementation 'io.micronaut.validation:micronaut-validation'
    implementation 'jakarta.annotation:jakarta.annotation-api'
    implementation 'org.yaml:snakeyaml'

    testImplementation 'org.spockframework:spock-core'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

gradlePlugin {
    plugins {
        register('grails-website') {
            id = 'grails-website'
            implementationClass = 'website.gradle.GrailsWebsitePlugin'
            displayName = 'Grails Website Plugin'
            description = 'Creates tasks to generate the Grails Website'
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed', 'standardOut', 'standardError')
    }
}